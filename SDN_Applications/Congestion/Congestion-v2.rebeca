reactiveclass Controller(4) { 
	knownrebecs {
		Switch s1;
		Switch s2;
		Switch s3;
		Switch s4;
		Switch s5;
	}
	statevars {
		boolean RoutingError;
	}
	Controller() {
		self.initRoutings();
		self.changeRoutingsF1() after(2);
		self.changeRoutingsF2() after(2);
     	RoutingError = false;
	}
	msgsrv initRoutings(){ 	//action: 1 for drop 0 for forward 		//protocol: 1 for SSH 0 for Normal
		//msgsrv modifyState(byte packetSrcId , byte packetDestId, byte port, byte action, byte protocol)
		
		// path = f1_before
		s1.modifyState(1, 5, 1, 0, 0) ;
		s2.modifyState(1, 5, 1, 0, 0) ;
		s5.modifyState(1, 5, 0, 0, 0) ;
		//path = f2_before
		s3.modifyState(3, 5, 2, 0, 0) ;
		s4.modifyState(3, 5, 2, 0, 0) ;
		s5.modifyState(3, 5, 0, 0, 0) ;
	}
	msgsrv changeRoutingsF1(){		// path = f1_after
		s1.modifyState(1, 5, 2, 0, 0);
		s4.modifyState(1, 5, 2, 0, 0);
		s5.modifyState(1, 5, 0, 0, 0);
	}
	msgsrv changeRoutingsF2() {		// path = f2_after
		s3.modifyState(3, 5, 1, 0, 0);
		s2.modifyState(3, 5, 1, 0, 0);
		s5.modifyState(3, 5, 0, 0, 0);
	}
	msgsrv newPacketInMessage(	byte switchId, byte packetSrcId , byte packetDestId, byte packetData, byte packetProtocol, byte SwitchPort){
		// This message is sent from a switch to the controller when the switch processes a packet
		// for which it has no action rule to apply.  

		if (switchId == 1 )// if unknown packet arrive from s1
		{
			//forward packet by action = 0 
			//s1.newPacketOutMessage(packetSrcId, packetDestId, packetData ,packetProtocol,SwitchPort, 0 );
			RoutingError = true;
		}
		else if (switchId == 2) // if packet arrive from s2 
		{
			//action = 1 means drop the packet
			//s2.newPacketOutMessage(packetSrcId, packetDestId, packetData ,packetProtocol,SwitchPort, 1 );
			RoutingError = true;
		}
		else if (switchId == 3) // if packet arrive from s3 
		{
			//action = 1 means drop the packet
			//s3.newPacketOutMessage(packetSrcId, packetDestId, packetData ,packetProtocol,SwitchPort, 1 );
			RoutingError = true;
		}
		else if (switchId == 4) // if packet arrive from s3 
		{
			//action = 1 means drop the packet
			//s3.newPacketOutMessage(packetSrcId, packetDestId, packetData ,packetProtocol,SwitchPort, 1 );
			RoutingError = true;
		}
		else if (switchId == 5) // if packet arrive from s3 
		{
			//action = 1 means drop the packet
			//s3.newPacketOutMessage(packetSrcId, packetDestId, packetData ,packetProtocol,SwitchPort, 1 );
			RoutingError = true;
		}
	}
}
reactiveclass Switch(4) { 
	knownrebecs {
		Controller controller;
	} 
	statevars {
		byte SwId;
		//int CurrentTime;
		int [3/*portCount*/] packetCountInEachPortInEachTime;
		Switch [2] neighbor_switches;
		Host host_connected_to_this_switch;
		
		byte [10] flowTableHeaderDestIP;
		byte [10] flowTableHeaderSrcIP;
		byte [10] flowTableHeaderProtocol;
		byte [10] flowTableOutPort;
		byte [10] flowTableActionType;
		byte [10] flowTablePrioriry; //can be a list- future work
						
		boolean illegalAccess;
		boolean liveness_error;
		boolean isRoutingChanged;
	}
	Switch(byte switchId, Host connectedHost , Switch neighbor_switches1, Switch neighbor_switches2) {
	    SwId = switchId;
	    neighbor_switches[0] = neighbor_switches1;
		neighbor_switches[1] = neighbor_switches2;
		host_connected_to_this_switch = connectedHost;
		
	    illegalAccess 		= false;
	    liveness_error 		= false;
		isRoutingChanged 	= false;
		
		FT_reset(); // reset Flow table array
    	//CurrentTime = 0;
     	int i;
     	for (i = 0 ; i < 3/*portCount*/ ; i++)
     	{
     		packetCountInEachPortInEachTime[i] = 0;
     	}
   	
	}
	msgsrv swHandlePacket(	byte packetSrcId , byte packetDestId, byte packetData, byte packetProtocol, byte SwitchPort) 
	{ 
	    boolean hasRuleInTable= false;

	    //lookup in flow table 
	    int i;
	    for (i = 0; i < 10; i = i + 1) 
	    {
	    	if (flowTableHeaderDestIP[i] 	== packetDestId 
	    	&& flowTableHeaderSrcIP[i] 		== packetSrcId 
	    	&& flowTableHeaderProtocol[i] 	== packetProtocol)
	    	//if match occurred 
	    	{
	    		hasRuleInTable = true;
				if (flowTableActionType[i] == 0) //forward
				{
					// Count Out packet
	    			packetCountInEachPortInEachTime[flowTableOutPort[i]]++;
	    			
					if (flowTableOutPort[i] == 0) // rules related to host
					{
						host_connected_to_this_switch.hostHandlePacket(	packetSrcId, packetDestId, packetData, packetProtocol, SwitchPort);
					}
					else if (flowTableOutPort[i] == 1) // rules related to neighbor_switches1
					{
    					neighbor_switches[0].swHandlePacket( packetSrcId ,  packetDestId,  packetData,  packetProtocol,  SwitchPort);
					}
					else // rules related to neighbor_switches2
					{
    					neighbor_switches[1].swHandlePacket( packetSrcId ,  packetDestId,  packetData,  packetProtocol,  SwitchPort);
					}
				} 
			}
		}
		if (hasRuleInTable == false) // New packet recieved without match in F.T.(Flow Table)
		{
			controller.newPacketInMessage(	SwId, packetSrcId , packetDestId, packetData, packetProtocol, SwitchPort);
		}
	}
	
	msgsrv newPacketOutMessage(	byte packetSrcId , byte packetDestId, byte packetData, byte packetProtocol, byte SwitchPort, byte action)
	{
	
		//This message is sent from the controller to a switch to notify 
		//that it must re-try applying an action rule to a buffered packet. 
		//The message includes the packet header.
		
		//insert new rule to flow table
		boolean ruleIsInserted;
		ruleIsInserted = insertRuleInFT(packetSrcId , packetDestId, SwitchPort, action, packetProtocol);
	    
	    if (ruleIsInserted == true){
	    	self.swHandlePacket(packetSrcId, packetDestId, packetData , packetProtocol, SwitchPort) ;
	    } 

		
	}

	msgsrv modifyState(byte packetSrcId , byte packetDestId, byte port, byte action, byte protocol)
	{
		insertRuleInFT( packetSrcId , packetDestId, port, action, protocol);
	}

	
	boolean FT_reset()
	{
		int i; 
     	for (i = 0; i < 10; i = i + 1) 
     	{
     		flowTableHeaderSrcIP[i]		= 0;
     		flowTableHeaderDestIP[i] 	= 0;
     		flowTableOutPort[i] 		= 0;
     		flowTableActionType[i] 		= 0;
     		flowTableHeaderProtocol[i] 	= 0;
     	}
	}
	
	boolean getMatchFields()
	{
	}
	
	boolean sortFlowTable()
	{
	}
	
	boolean insertRuleInFT(byte packetSrcId , byte packetDestId, byte port, byte action, byte protocol)
	{
			//action 1 = drop & 0 = forward
			//new action rules to be inserted into the switch\92s flow-table
			//insert new rule to flow table
			boolean ruleIsInserted;
			ruleIsInserted = false;
			int i;
			for (i = 0; i < 10; i = i + 1) {
	    		if (ruleIsInserted == false) {
	    			if (flowTableHeaderDestIP[i] == 0 || (flowTableHeaderSrcIP[i] == packetSrcId && flowTableHeaderDestIP[i] == packetDestId && flowTableHeaderProtocol[i] == protocol)) {
	    				flowTableHeaderDestIP[i] 		= packetDestId;
	    				flowTableHeaderSrcIP[i] 		= packetSrcId;
	    				flowTableOutPort[i] 			= port;
	    				flowTableHeaderProtocol[i] 		= protocol;
	    				flowTableActionType[i] 			= action;
	    				ruleIsInserted = true;
	    			}
	    		}
	    	}
	   		sortFlowTable();
	   		return ruleIsInserted;
	}
	
}
reactiveclass Host(4) { 
	knownrebecs {
		Switch knownSwitch;
 	}
	statevars { 
		byte id; 
		boolean illegalAccess;
		boolean liveness_error;
		boolean isRoutingChanged;
		byte 	knownSwitchPort;  
		int [2] packet_counter;

	}
	Host(byte hostId , byte SwitchPortNumber) { 
		id = hostId;
		knownSwitchPort 	= SwitchPortNumber; 
		illegalAccess 		= false;
		liveness_error 		= false;
		isRoutingChanged 	= false;
		int i;
		for (i = 0; i < 2 ; i = i+1)
			packet_counter[i] = 0;
		
		// Represantative packets will be send from h1 & h3 to destination h5
		if (id == 1) { 
			// send from host with ip = 1 to destination host with ip = 5 packetData =1 with protocol = 0
			self.sendInPacket(1, 5, 1, 0) after(1); // should go through path = f1 
			self.sendInPacket(1, 5, 3, 0) after(2);
		}
		if (id == 3)
		{
			self.sendInPacket(3, 5, 2, 0) after(1); // should go through path = f2
			self.sendInPacket(3, 5, 4, 0) after(2);
		} 
	} 
	msgsrv sendInPacket(byte packetSrcId , byte packetDestId, byte packetData, byte packetProtocol) {
		knownSwitch.swHandlePacket(	packetSrcId, packetDestId, packetData, packetProtocol , knownSwitchPort) ; 
	}
	msgsrv hostHandlePacket(byte packetSrcId , byte packetDestId, byte packetData, byte packetProtocol, byte SwitchPort) {
		
		if (id == 1  ) 
		{
			// packetData is = uniqe combination of source and destination for find policy violations
			if ( packetData == 1  || packetData == 2 )
			{
				illegalAccess = true;
			}
		}
		else if (id == 2 )
		{
			// packetData is = uniqe combination of source and destination for find policy violations
			if ( packetData == 1  || packetData == 2 )
			{
				illegalAccess = true;
			}
		}
		else if (id == 3)
		{
			// packetData is = uniqe combination of source and destination for find policy violations
			if ( packetData == 1  || packetData == 2 )
			{
				illegalAccess = true;
			}
		}
		else if (id == 4)
		{
			// packetData is = uniqe combination of source and destination for find policy violations
			if ( packetData == 1  || packetData == 2 )
			{
				illegalAccess = true;
			}
		}
		else if (id == 5)
		{
			// packetData is = uniqe combination of source and destination for find policy violations
			//packet_counter[packetData-1] = packet_counter[packetData-1] + 1;
			if (packetData == 1 )
			{
				packet_counter[0]=packet_counter[0]+1;
			}
			
			if ( packetData == 2)
			{
				packet_counter[1]= packet_counter[1]+1;
			}

		}		
		if (packetData == 1)// || packetData == 2) //for check all state without deadlock
			self.done() after(4);
	}
	msgsrv done() {
		if ( (packet_counter[0] != 1) && (packet_counter[1] != 1) )
			liveness_error = true;
	
		self.done() after(4);
	}
}
main { 
	
	Controller controller(s1 , s2 , s3 , s4 , s5):(); 
   	
   	//Switch has 3 port ={0,1,2} 
   	//All hosts connected to port 0 of switches with same id number for example h1 connected to port 0 of s1
   	//parameters ordered = switchId , 5 pair of ( host and id )
   	//mission is prevent packets reach from untrusted host to trusted hosts without request from trusted hosts.
	Switch s1(controller):(1, h1, s2, s4); 
	Switch s2(controller):(2, h2, s5, s3);
	Switch s3(controller):(3, h3, s2, s4);
	Switch s4(controller):(4, h4, s3, s5); 
	Switch s5(controller):(5, h5, s2, s4); 
 
	Host h1(s1):(1,0); // connect host c1 to switch s1 by id = 1 to port = 0 of switch
	Host h2(s2):(2,0); // connect host c2 to switch s1 by id = 2 to port = 0 of switch
	Host h3(s3):(3,0); // connect host c3 to switch s1 by id = 3 to port = 0 of switch
	Host h4(s4):(4,0); // connect host c4 to switch s1 by id = 4 to port = 0 of switch
	Host h5(s5):(5,0); // connect host c5 to switch s1 by id = 5 to port = 0 of switch
}

